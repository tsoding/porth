// In progress rewrite of ./porth.py in Porth

include "std.porth"

macro MEM_CAPACITY 640000 end
macro SIM_STACK_CAP 1024 end

macro OP_PUSH_INT 0 end
macro OP_PLUS     1 end
macro OP_MINUS    2 end
macro OP_MUL      3 end
macro OP_PRINT    4 end
macro OP_DUP      5 end
macro OP_DROP     6 end
macro COUNT_OPS   7 end

macro OPS_CAP       1024 end
macro sizeof(Op)    16 end
macro Op.type       0 + end
macro Op.operand    8 + end
macro @Op.type      Op.type @64 end
macro !Op.type      Op.type !64 end
macro @Op.operand   Op.operand @64 end
macro !Op.operand   Op.operand !64 end

memory file_path_cstr  sizeof(ptr)                 end
memory fd              sizeof(u64)                 end
memory statbuf         sizeof(stat)                end
memory content         sizeof(Str)                 end
memory line            sizeof(Str)                 end
memory word            sizeof(Str)                 end
memory line_number     sizeof(u64)                 end
memory line_start      sizeof(ptr)                 end
memory sim-stack-count sizeof(u64)                 end
memory sim-stack       sizeof(u64) SIM_STACK_CAP * end
memory ops-count       sizeof(u64)                 end
memory ops             sizeof(Op) OPS_CAP *        end

memory out_fd sizeof(u64) end
macro @out_fd out_fd @64 end
macro !out_fd out_fd !64 end

memory empty_envp sizeof(ptr) end
memory nasm_argv sizeof(ptr) 4 * end
// TODO: search for external utilities in $PATH
"/usr/bin/nasm"c nasm_argv 0 8 * + !64
"-felf64"c       nasm_argv 1 8 * + !64
"output.asm"c    nasm_argv 2 8 * + !64

memory ld_argv sizeof(ptr) 5 * end
"/usr/bin/ld"c ld_argv 0 8 * + !64
"-o"c          ld_argv 1 8 * + !64
"output"c      ld_argv 2 8 * + !64
"output.o"c    ld_argv 3 8 * + !64

memory output_argv sizeof(ptr) 2 * end
"./output"c output_argv 0 8 * + !64

memory wstatus sizeof(u64) end
macro cmd_echoed // argv
  "[CMD]" puts
  dup while dup @64 0 != do
    " " puts
    // TODO: properly escape the logged CMD
    dup @64 cast(ptr) cstr-to-str puts
    8 +
  end drop
  "\n" puts

  fork
  
  if dup 0 = do
    drop
    dup @64 cast(ptr) empty_envp
    rot rot
    execve
    if dup 0 < do
      "[ERROR] could not exec external program\n" eputs
      1 exit
    end
  elif dup 0 > do
    drop
    // TODO: handle the result of wait4
    NULL 0 wstatus -1 wait4 drop
  else
    drop
    "[ERROR] could not fork a child\n" eputs
    1 exit
  end
  
  drop
end

macro sim-stack-push // u64 --
  if sim-stack-count @64 SIM_STACK_CAP >= do
    here eputs ": ERROR: data stack overflow in simulation mode\n" eputs 1 exit
  end
  sim-stack sim-stack-count @64 8 * + !64
  sim-stack-count inc64
end

macro sim-stack-pop // -- u64
  if sim-stack-count @64 0 = do
    here eputs ": ERROR: data stack underflow in simulation mode\n" eputs 1 exit
  end
  sim-stack-count dec64
  sim-stack sim-stack-count @64 8 * + @64
end

macro try_to_parse_word_as_int_or_fail_as_unknown_word // n1 s1 - ret
  0 0 while dup word @Str.count < do
    dup word @Str.data + @8

    if dup isdigit lnot do
      file_path_cstr @64 cast(ptr) cstr-to-str eputs
      ":" puts line_number @64 putd
      ":" puts word @Str.data cast(int) line_start @64 - 1 + putd
      ": ERROR: `" eputs word @Str eputs "` is unknown word\n" eputs
      1 exit
    end

    '0' -
    rot 10 * +
    swap
    1 +
  end drop
end


macro push-op // type operand -- 
  // TODO: assert OPS_CAP
  ops-count @64 sizeof(Op) * ops +
  dup rot swap !Op.operand
  !Op.type
  ops-count inc64
end

macro print-op-type
  if COUNT_OPS 7 != do
    here eputs ": Assertion Failed: Exhaustive handling of Op types in print-op-type\n" eputs
    1 exit
  end

  if dup OP_PUSH_INT = do
    "OP_PUSH_INT" puts
  elif dup OP_PLUS = do
    "OP_PLUS" puts
  elif dup OP_MINUS = do
    "OP_MINUS" puts
  elif dup OP_MUL = do
    "OP_MUL" puts
  elif dup OP_DUP = do
    "OP_DUP" puts
  elif dup OP_DROP = do
    "OP_DROP" puts
  elif dup OP_PRINT = do
    "OP_PRINT" puts
  else
    here eputs ": Unknown op type\n" eputs 1 exit
  end
  drop
end

macro dump-ops // --
  0 while dup ops-count @64 < do
    // ptr ptr
    dup sizeof(Op) * ops +
    "Type:    " puts dup @Op.type print-op-type "\n" puts
    "Operand: " puts @Op.operand putd           "\n" puts
    "----------\n" puts
    1 +
  end
  drop
end

macro compile-ops // --
  "[INFO] Generating output.asm\n" puts

  420                  // mode
  O_CREAT O_WRONLY or  // flags
  // TODO: the output file path should be based on the input file path
  "output.asm"c        // pathname
  AT_FDCWD
  openat
  !out_fd

  if @out_fd 0 < do
    "[ERROR] could not open `output.asm`\n" eputs
    1 exit
  end

  "BITS 64\n"                              @out_fd fputs
  "segment .text\n"                        @out_fd fputs
  "print:\n"                               @out_fd fputs
  "    mov     r9, -3689348814741910323\n" @out_fd fputs
  "    sub     rsp, 40\n"                  @out_fd fputs
  "    mov     BYTE [rsp+31], 10\n"        @out_fd fputs
  "    lea     rcx, [rsp+30]\n"            @out_fd fputs
  ".L2:\n"                                 @out_fd fputs
  "    mov     rax, rdi\n"                 @out_fd fputs
  "    lea     r8, [rsp+32]\n"             @out_fd fputs
  "    mul     r9\n"                       @out_fd fputs
  "    mov     rax, rdi\n"                 @out_fd fputs
  "    sub     r8, rcx\n"                  @out_fd fputs
  "    shr     rdx, 3\n"                   @out_fd fputs
  "    lea     rsi, [rdx+rdx*4]\n"         @out_fd fputs
  "    add     rsi, rsi\n"                 @out_fd fputs
  "    sub     rax, rsi\n"                 @out_fd fputs
  "    add     eax, 48\n"                  @out_fd fputs
  "    mov     BYTE [rcx], al\n"           @out_fd fputs
  "    mov     rax, rdi\n"                 @out_fd fputs
  "    mov     rdi, rdx\n"                 @out_fd fputs
  "    mov     rdx, rcx\n"                 @out_fd fputs
  "    sub     rcx, 1\n"                   @out_fd fputs
  "    cmp     rax, 9\n"                   @out_fd fputs
  "    ja      .L2\n"                      @out_fd fputs
  "    lea     rax, [rsp+32]\n"            @out_fd fputs
  "    mov     edi, 1\n"                   @out_fd fputs
  "    sub     rdx, rax\n"                 @out_fd fputs
  "    xor     eax, eax\n"                 @out_fd fputs
  "    lea     rsi, [rsp+32+rdx]\n"        @out_fd fputs
  "    mov     rdx, r8\n"                  @out_fd fputs
  "    mov     rax, 1\n"                   @out_fd fputs
  "    syscall\n"                          @out_fd fputs
  "    add     rsp, 40\n"                  @out_fd fputs
  "    ret\n"                              @out_fd fputs
  "global _start\n"                        @out_fd fputs
  "_start:\n"                              @out_fd fputs
  "    mov [args_ptr], rsp\n"              @out_fd fputs

  0 while dup ops-count @64 < do
    dup sizeof(Op) * ops +

    // TODO: compile time assertion
    if COUNT_OPS 7 != do
      here eputs ": Assertion Failed: Exhaustive handling of Op types in compile-ops\n" eputs
      1 exit
    end

    if dup @Op.type OP_PUSH_INT = do
       "    ;; -- push int "  @out_fd fputs dup @Op.operand @out_fd fputd " --\n" @out_fd fputs
       "    mov rax, "        @out_fd fputs dup @Op.operand @out_fd fputd "\n"    @out_fd fputs
       "    push rax\n"       @out_fd fputs
    elif dup @Op.type OP_PLUS = do
       "    ;; -- plus --\n"  @out_fd fputs
       "    pop rax\n"        @out_fd fputs
       "    pop rbx\n"        @out_fd fputs
       "    add rax, rbx\n"   @out_fd fputs
       "    push rax\n"       @out_fd fputs
    elif dup @Op.type OP_MINUS = do
       "    ;; -- minus --\n" @out_fd fputs
       "    pop rax\n"        @out_fd fputs
       "    pop rbx\n"        @out_fd fputs
       "    sub rbx, rax\n"   @out_fd fputs
       "    push rbx\n"       @out_fd fputs
    elif dup @Op.type OP_MUL = do
       "    ;; -- mul --\n"   @out_fd fputs
       "    pop rax\n"        @out_fd fputs
       "    pop rbx\n"        @out_fd fputs
       "    mul rbx\n"        @out_fd fputs
       "    push rax\n"       @out_fd fputs
    elif dup @Op.type OP_PRINT = do
       "    ;; -- print --\n" @out_fd fputs
       "    pop rdi\n"        @out_fd fputs
       "    call print\n"     @out_fd fputs
    elif dup @Op.type OP_DUP = do
       "    ;; -- dup --\n"   @out_fd fputs
       "    pop rax\n"        @out_fd fputs
       "    push rax\n"       @out_fd fputs
       "    push rax\n"       @out_fd fputs
    elif dup @Op.type OP_DROP = do
       "    ;; -- drop --\n"  @out_fd fputs
       "    pop rax\n"        @out_fd fputs
    else
       here eputs ": unreachable\n" eputs 1 exit
    end

    drop

    1 +
  end
  drop

  "    mov rax, 60\n"  @out_fd fputs
  "    mov rdi, 0\n"   @out_fd fputs
  "    syscall\n"      @out_fd fputs
  "segment .bss\n"     @out_fd fputs
  "args_ptr: resq 1\n" @out_fd fputs
  "mem: resb "         @out_fd fputs MEM_CAPACITY @out_fd fputd "\n" @out_fd fputs

  @out_fd close drop

  nasm_argv cmd_echoed
  ld_argv cmd_echoed
  output_argv cmd_echoed
end  

macro simulate-ops // --
  0 while dup ops-count @64 < do
    dup sizeof(Op) * ops +

    if COUNT_OPS 7 != do
      here eputs ": Assertion Failed: Exhaustive handling of Op types in simulate-ops\n" eputs
      1 exit
    end

    if dup @Op.type OP_PUSH_INT = do
       dup @Op.operand sim-stack-push
    elif dup @Op.type OP_PLUS = do
       sim-stack-pop
       sim-stack-pop
       +
       sim-stack-push
    elif dup @Op.type OP_MINUS = do
       sim-stack-pop
       sim-stack-pop
       swap
       -
       sim-stack-push
    elif dup @Op.type OP_MUL = do
       sim-stack-pop
       sim-stack-pop
       *
       sim-stack-push
    elif dup @Op.type OP_PRINT = do
       sim-stack-pop print
    elif dup @Op.type OP_DUP = do
       sim-stack-pop
       dup
       sim-stack-push
       sim-stack-push
    elif dup @Op.type OP_DROP = do
       sim-stack-pop
       drop
    else
       here eputs ": unreachable\n" eputs 1 exit
    end

    drop

    1 +
  end
  drop
end

macro parse_file_path_cstr_into_ops
  0                            // mode
  O_RDONLY                     // flags
  file_path_cstr @64 cast(ptr) // pathname
  AT_FDCWD                     // dirfd
  openat

  if dup 0 < do
    "ERROR: could not open file " eputs file_path_cstr @64 cast(ptr) cstr-to-str eputs "\n" eputs
    1 exit
  end

  fd !64

  if statbuf fd @64 fstat 0 < do
    "ERROR: could not determine the size of file " eputs file_path_cstr @64 cast(ptr) cstr-to-str eputs "\n" eputs
    1 exit
  end

  statbuf @stat.st_size content !Str.count

  0                        // offset
  fd @64                   // fd
  MAP_PRIVATE              // flags
  PROT_READ                // prot
  content @Str.count       // length
  NULL                     // addr
  mmap
  content !Str.data

  if content @Str.data cast(int) 0 < do
    "ERROR: could not memory map file " eputs file_path_cstr @64 cast(ptr) cstr-to-str eputs "\n" eputs
    1 exit
  end

  1 line_number !64
  while content @Str.count 0 > do
    '\n' line content str-chop-by-delim
    line @Str.data line_start !64
    while line @Str.count 0 > do
       line str-trim-left
       ' ' word line str-chop-by-delim

       if COUNT_OPS 7 != do
         here eputs ": Assertion Failed: Exhaustive handling of Op types in parse-file-path\n" eputs
         1 exit
       end

       if word @Str "+" streq do
         OP_PLUS 0 push-op
       elif word @Str "-" streq do
         OP_MINUS 0 push-op
       elif word @Str "*" streq do
         OP_MUL 0 push-op
       elif word @Str "print" streq do
         OP_PRINT 0 push-op
       elif word @Str "dup" streq do
         OP_DUP 0 push-op
       elif word @Str "drop" streq do
         OP_DROP 0 push-op
       else
         OP_PUSH_INT try_to_parse_word_as_int_or_fail_as_unknown_word push-op
       end
    end
    line_number inc64
  end
  // TODO: parse_file_path does not clean up resources after itself
end

macro usage // --
  dup "Usage: porth <SUBCOMMAND>\n" rot fputs
  dup "  SUBCOMMANDS:\n" rot fputs
  dup "    sim <file>       Simulate the program.\n" rot fputs
  // TODO: -r flag for com subcommand
  dup "    com <file>       Compile the program\n" rot fputs
  dup "    dump <file>      Dump the ops of the program\n" rot fputs
  dup "    help             Print this help to stdout and exit with 0 code\n" rot fputs
  drop
end

macro main // --
  if argc 2 < do
    stderr usage
    "ERROR: subcommand is not provided\n" eputs
    1 exit
  end

  1 nth_argv
  if dup "sim"c cstreq do
    if argc 3 < do
      stderr usage
      "ERROR: no input file is provided for the `sim` subcommand\n" eputs
      1 exit
    end

    2 nth_argv file_path_cstr !64

    parse_file_path_cstr_into_ops

    simulate-ops
  elif dup "com"c cstreq do
    if argc 3 < do
      stderr usage
      "ERROR: no input file is provided for the `com` subcommand\n" eputs
      1 exit
    end

    2 nth_argv file_path_cstr !64

    parse_file_path_cstr_into_ops

    compile-ops
  elif dup "help"c cstreq do
    stdout usage
    0 exit
  elif dup "dump"c cstreq do
    if argc 3 < do
      stderr usage
      "ERROR: no input file is provided for the `dump` subcommand\n" eputs
      1 exit
    end

    2 nth_argv file_path_cstr !64

    parse_file_path_cstr_into_ops

    dump-ops
  else
    stderr usage
    "ERROR: unknown subcommand `" eputs dup cstr-to-pstr eputs "`\n" eputs
    1 exit
  end
  drop
end

main
