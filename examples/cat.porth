include "std.porth"

memory fd sizeof(u64) end
macro @fd fd @64 end
macro !fd fd !64 end

macro BUFFER_CAP 1024 end
memory buffer BUFFER_CAP end

memory file_path_cstr sizeof(ptr) end
macro @file_path_cstr file_path_cstr @64 cast(ptr) end
macro !file_path_cstr file_path_cstr !64 end

macro cat_fd
  while BUFFER_CAP buffer @fd read dup 0 > do
     buffer puts
  end drop
end

argc 2 < if
  stdin !fd
  cat_fd
else
  1 while dup argc < do
    dup nth_argv !file_path_cstr

    0 O_RDONLY @file_path_cstr AT_FDCWD openat
    
    dup 0 < if
      "ERROR: could not open file " eputs
      @file_path_cstr cstr-to-str eputs
      "\n" eputs
      drop
    else
      fd !64
      cat_fd
      fd @64 close drop
    end
  
    1 +
  end drop
end
