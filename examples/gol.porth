include "std.porth"

// constants
macro ROWS 10 end
macro COLS 20 end
macro BOARD_SIZE ROWS COLS * end
macro PUTD_BUFFER_CAP 32 end

// memory layout
macro putd_buffer mem end
macro delta_time putd_buffer PUTD_BUFFER_CAP + end
macro board_current_index delta_time 16 + end
macro nbors board_current_index 8 + end
macro value nbors 8 + end
macro board_base value 8 + end
macro display BOARD_SIZE 2 * board_base + end

macro putd
  dup 0 = if
    "0" puts
  else
    putd_buffer PUTD_BUFFER_CAP +
    while over 0 > do
      1 - dup rot
      10 divmod
      rot swap '0' + . swap
    end

    dup
    putd_buffer PUTD_BUFFER_CAP + swap - swap puts
  end
  drop
end

macro board_current
  board_base board_current_index ,64 BOARD_SIZE * +
end

macro board_next
  board_base 1 board_current_index ,64 - BOARD_SIZE * +
end

macro swap_boards
  board_current_index 1 board_current_index ,64 - .64
end

macro display_row
  0 while dup COLS < do
    2dup + , 0 = if
      display over + '.' .
    else
      display over + '#' .
    end
    1 +
  end drop
  COLS display + '\n' .
  COLS 1 + display puts
  drop
end

macro display_board
  0 while dup ROWS < do
    2dup COLS * + display_row
    1 +
  end drop
  drop
end

macro display_current_board
  board_current display_board
end

macro get_current_cell
  swap COLS * + board_current + ,
end

macro set_next_cell
  value swap .64
  swap COLS * + board_next +
  value ,64
  .
end

macro in_bounds
  dup 0 >= swap COLS < and
  swap
  dup 0 >= swap ROWS < and
  and
end

macro count_current_nbors
  nbors 0 .64

  2dup 1 - swap 1 - swap 2dup in_bounds
  rot rot swap COLS * + board_current + , 1 =
  and if nbors inc64 end

  2dup 1 - 2dup in_bounds
  rot rot swap COLS * + board_current + , 1 =
  and if nbors inc64 end

  2dup 1 - swap 1 + swap 2dup in_bounds
  rot rot swap COLS * + board_current + , 1 =
  and if nbors inc64 end

  2dup swap 1 - swap 2dup in_bounds
  rot rot swap COLS * + board_current + , 1 =
  and if nbors inc64 end

  2dup swap 1 + swap 2dup in_bounds
  rot rot swap COLS * + board_current + , 1 =
  and if nbors inc64 end

  2dup 1 + swap 1 - swap 2dup in_bounds
  rot rot swap COLS * + board_current + , 1 =
  and if nbors inc64 end

  2dup 1 + 2dup in_bounds
  rot rot swap COLS * + board_current + , 1 =
  and if nbors inc64 end

  2dup 1 + swap 1 + swap 2dup in_bounds
  rot rot swap COLS * + board_current + , 1 =
  and if nbors inc64 end

  2drop
  nbors ,64
end

macro compute_next_board
  0 while dup ROWS < do
    0 while dup COLS < do
      2dup get_current_cell 1 = if
        2dup count_current_nbors
        dup 2 = swap 3 = or if
          2dup 1 set_next_cell
        else
          2dup 0 set_next_cell
        end
      else
        2dup count_current_nbors 3 = if
          2dup 1 set_next_cell
        else
          2dup 0 set_next_cell
        end
      end

      1 +
    end drop
    1 +
  end drop
end

// .*.
// ..*
// ***
macro put_glider
   dup 0 COLS * 1 + + 1 .
   dup 1 COLS * 2 + + 1 .
   dup 2 COLS * 0 + + 1 .
   dup 2 COLS * 1 + + 1 .
   dup 2 COLS * 2 + + 1 .
   drop
end

macro main
  delta_time 8 + 100000000 .64

  board_current put_glider

  while true do
    display_current_board
    compute_next_board
    swap_boards

    NULL delta_time 0 CLOCK_MONOTONIC clock_nanosleep drop
    "\033[" puts ROWS putd "A" puts
    "\033[" puts COLS putd "D" puts
  end
end

main
