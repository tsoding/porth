include "std.porth"

macro N 20 end

memory acc sizeof(u64) N * end
memory tmp sizeof(u64) N * end
memory ans sizeof(u64) end

2 while dup N <= do

  // clean up the tmp table
  0 while dup N < do
    tmp over 8 * + 0 swap !64
    1 +
  end drop
  
  // factorize
  dup 2 while over 1 > do
    if 2dup mod 0 = do
      dup 8 * tmp + inc64
      swap over / swap
    else
      1 +
    end
  end 2drop

  0 while dup N < do
    if dup 8 * acc + @64
       over 8 * tmp + @64
       <
    do
      dup 8 * acc +
      over 8 * tmp + @64
      swap !64
    end

    1 +
  end drop

  1 +
end drop

1 ans !64

0 while dup N < do
  acc over 8 * + @64 while dup 0 > do
    over ans @64 * ans !64
    1 -
  end drop

  1 +
end drop

ans @64 print
