include "std.porth"

// TODO: this solution requires more than 640kb of memory
// Add a test case when there is an opportunity to customize that

macro sizeof(u64) 8 end
macro ans mem end
macro primes-count ans 8 + end
macro primes primes-count 8 + end

macro push-prime // value --
  primes-count @64 sizeof(u64) * primes + !64
  primes-count inc64
end

macro is-prime // [value: int] -> [ret: bool]
  0 while
    if 2dup sizeof(u64) * primes + @64 dup * >= do
      2dup sizeof(u64) * primes + @64 mod 0 !=
    else false end
  do 1 + end
  sizeof(u64) * primes + @64 dup * <    
end

2 push-prime
2 ans !64

3 while dup 2000000 < do
  if dup is-prime do
    dup push-prime
    dup ans @64 + ans !64
  end
  1 +
end drop

ans @64 print
